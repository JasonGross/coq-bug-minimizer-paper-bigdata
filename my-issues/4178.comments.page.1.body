Comment author: @JasonGross

I don't want to have to manually write lemmas like this:

```coq
Require Import Coq.Lists.List.
Require Import Coq.Strings.String.

Global Coercion is_true : bool >-> Sortclass.
Coercion bool_of_sumbool {A B} (x : {A} + {B}) : bool := if x then true else false.

Scheme Equality for bool.
Scheme Equality for Ascii.ascii.
Scheme Equality for string.
Scheme Equality for list.
Scheme Equality for option.

Section beq_correct.
  Local Ltac t rew_t :=
    match goal with
      | [ |- _ = bool_of_sumbool (?eq_dec ?x ?y) ]
        => revert y; induction x; intro y; simpl
    end;
    match goal with
      | [ |- _ = bool_of_sumbool (?eq_dec ?x ?y) ]
        => destruct (eq_dec x y)
    end;
    subst; simpl in *;
    try solve [ repeat match goal with
                         | [ H : _ <> ?y |- _ ] => is_var y; destruct y
                         | [ H : ?x <> ?x |- _ ] => destruct (H eq_refl)
                         | [ H : _ |- _ ] => rewrite !H; []
                         | _ => progress rew_t
                         | _ => progress simpl in *
                         | _ => split; (congruence || discriminate)
                         | _ => progress subst
                         | [ |- context[bool_of_sumbool ?e] ]
                           => destruct e; simpl
                         | [ |- true = false ] => exfalso
                         | [ |- false = true ] => exfalso
                         | [ H : _ <> _ |- False ] => apply H; clear H
                         | [ |- ?x = false ] => case_eq x; intro
                       end ].

  Lemma bool_beq_correct {x y} : bool_beq x y = bool_eq_dec x y.
  Proof. t idtac. Qed.
  Lemma ascii_beq_correct {x y} : ascii_beq x y = ascii_eq_dec x y.
  Proof. t ltac:(rewrite !bool_beq_correct). Qed.
  Lemma string_beq_correct {x y} : string_beq x y = string_eq_dec x y.
  Proof. t ltac:(rewrite !ascii_beq_correct). Qed.
  Lemma list_beq_correct {A eq_A}
        (A_bl : forall x y : A, eq_A x y = true -> x = y)
        (A_lb : forall x y : A, x = y -> eq_A x y = true)
        {x y : list A}
  : list_beq eq_A x y = list_eq_dec eq_A A_bl A_lb x y.
  Proof.
    t ltac:(first [ rewrite !(A_lb _ _) by congruence
                  | erewrite (A_bl _ _) by eassumption
                  | rewrite Bool.andb_true_r
                  | rewrite Bool.andb_false_r ]).
  Qed.
  Definition list_eq_dec' {A} (dec_eq_A : forall x y : A, {x = y} + {x <> y})
  : forall x y : list A, {x = y} + {x <> y}.
  Proof.
    refine (list_eq_dec dec_eq_A _ _);
    abstract (intros; edestruct dec_eq_A; simpl in *; subst; congruence).
  Defined.
  Lemma list_beq_correct' {A} {eq_A : A -> A -> bool} {dec_eq_A : forall x y : A, {x = y} + {x <> y}}
        (eq_A_correct : forall x y, eq_A x y = dec_eq_A x y)
        {x y : list A}
  : list_beq eq_A x y = list_eq_dec' dec_eq_A x y.
  Proof.
    unfold list_eq_dec'; erewrite list_beq_correct.
    do 2 edestruct list_eq_dec; subst; simpl; congruence.
    Grab Existential Variables.
    intros ??; rewrite eq_A_correct; edestruct dec_eq_A; simpl; congruence.
    intros ??; rewrite eq_A_correct; edestruct dec_eq_A; simpl; congruence.
  Qed.
  Lemma option_beq_correct {A eq_A}
        (A_bl : forall x y : A, eq_A x y = true -> x = y)
        (A_lb : forall x y : A, x = y -> eq_A x y = true)
        {x y : option A}
  : option_beq eq_A x y = option_eq_dec eq_A A_bl A_lb x y.
  Proof.
    t ltac:(first [ rewrite !(A_lb _ _) by congruence
                  | erewrite (A_bl _ _) by eassumption
                  | rewrite Bool.andb_true_r
                  | rewrite Bool.andb_false_r ]).
  Qed.
  Definition option_eq_dec' {A} (dec_eq_A : forall x y : A, {x = y} + {x <> y})
  : forall x y : option A, {x = y} + {x <> y}.
  Proof.
    refine (option_eq_dec dec_eq_A _ _);
    abstract (intros; edestruct dec_eq_A; simpl in *; subst; congruence).
  Defined.
  Lemma option_beq_correct' {A} {eq_A : A -> A -> bool} {dec_eq_A : forall x y : A, {x = y} + {x <> y}}
        (eq_A_correct : forall x y, eq_A x y = dec_eq_A x y)
        {x y : option A}
  : option_beq eq_A x y = option_eq_dec' dec_eq_A x y.
  Proof.
    unfold option_eq_dec'; erewrite option_beq_correct.
    do 2 edestruct option_eq_dec; subst; simpl; congruence.
    Grab Existential Variables.
    intros ??; rewrite eq_A_correct; edestruct dec_eq_A; simpl; congruence.
    intros ??; rewrite eq_A_correct; edestruct dec_eq_A; simpl; congruence.
  Qed.
End beq_correct.

```
Comment author: @JasonGross

Actually, given the way the [*_eq_dec] lemmas are set up, the following are probably the relevant correctness lemmas:
```coq
Section beq_bl_lb.
  Local Ltac t rew_t :=
    let x := match goal with
               | [ |- _ -> ?beq ?x ?y = true ] => constr:x
               | [ |- _ -> ?x = ?y ] => constr:x
             end in
    let y := match goal with
               | [ |- _ -> ?beq ?x ?y = true ] => constr:y
               | [ |- _ -> ?x = ?y ] => constr:y
             end in
    revert y; induction x; intro y; destruct y;
    simpl in *;
    subst;
    repeat match goal with
             | _ => progress subst
             | _ => congruence
             | _ => setoid_rewrite Bool.andb_true_iff
             | [ H : forall y, _ -> _ = true |- _ ] => setoid_rewrite <- (H _ : impl _ _)
             | [ H : forall y, _ = true -> _ = _ |- _ ] => setoid_rewrite (H _ : impl _ _)
             | [ H : forall x y, _ -> _ = true |- _ ] => setoid_rewrite <- (H _ _ : impl _ _)
             | [ H : forall x y, _ = true -> _ = _ |- _ ] => setoid_rewrite (H _ _ : impl _ _)
             | [ H : _ /\ _ |- _ ] => destruct H
             | [ |- _ /\ _ ] => split
             | _ => progress rew_t
           end.

  Lemma bool_bl {x y} : bool_beq x y = true -> x = y.
  Proof. t idtac. Qed.
  Lemma bool_lb {x y} : x = y -> bool_beq x y = true.
  Proof. t idtac. Qed.
  Lemma ascii_bl {x y} : ascii_beq x y = true -> x = y.
  Proof. t ltac:(setoid_rewrite (@ bool_bl _ _ : impl _ _) || intro). Qed.
  Lemma ascii_lb {x y} : x = y -> ascii_beq x y = true.
  Proof. t ltac:(setoid_rewrite <- (@ bool_lb _ _ : impl _ _) || intro). Qed.
  Lemma string_bl {x y} : string_beq x y = true -> x = y.
  Proof. t ltac:(setoid_rewrite (@ ascii_bl _ _ : impl _ _) || intro). Qed.
  Lemma string_lb {x y} : x = y -> string_beq x y = true.
  Proof. t ltac:(setoid_rewrite <- (@ ascii_lb _ _ : impl _ _) || intro). Qed.
  Lemma list_bl {A eq_A} (A_bl : forall x y : A, eq_A x y = true -> x = y) {x y} 
  : list_beq eq_A x y = true -> x = y.
  Proof. t intro. Qed.
  Lemma list_lb {A eq_A} (A_lb : forall x y : A, x = y -> eq_A x y = true) {x y} 
  : x = y -> list_beq eq_A x y = true.
  Proof. t intro. Qed.
  Lemma option_bl {A eq_A} (A_bl : forall x y : A, eq_A x y = true -> x = y) {x y} 
  : option_beq eq_A x y = true -> x = y.
  Proof. t intro. Qed.
  Lemma option_lb {A eq_A} (A_lb : forall x y : A, x = y -> eq_A x y = true) {x y} 
  : x = y -> option_beq eq_A x y = true.
  Proof. t intro. Qed.
End beq_bl_lb.

```
Comment author: @silene

I am a bit confused. The lemmas generated by Scheme Equality seem to be quite close to the ones you are asking for. Sure, their names are awfully long, but they don't warrant doing the proofs by hand:
```coq
Lemma bool_bl {x y} : bool_beq x y = true -> x = y.
Proof. now apply internal_bool_dec_bl. Qed.
Lemma bool_lb {x y} : x = y -> bool_beq x y = true.
Proof. now apply internal_bool_dec_lb. Qed.
Lemma ascii_bl {x y} : ascii_beq x y = true -> x = y.
Proof. now apply internal_ascii_dec_bl. Qed.
Lemma ascii_lb {x y} : x = y -> ascii_beq x y = true.
Proof. now apply internal_ascii_dec_lb. Qed.
Lemma string_bl {x y} : string_beq x y = true -> x = y.
Proof. now apply internal_string_dec_bl. Qed.
Lemma string_lb {x y} : x = y -> string_beq x y = true.
Proof. now apply internal_string_dec_lb. Qed.
Lemma list_bl {A eq_A} (A_bl : forall x y : A, eq_A x y = true -> x = y) {x y} 
  : list_beq A eq_A x y = true -> x = y.
Proof. now apply internal_list_dec_bl. Qed.
Lemma list_lb {A eq_A} (A_lb : forall x y : A, x = y -> eq_A x y = true) {x y} 
  : x = y -> list_beq A eq_A x y = true.
Proof. now apply internal_list_dec_lb. Qed.
Lemma option_bl {A eq_A} (A_bl : forall x y : A, eq_A x y = true -> x = y) {x y} 
  : option_beq A eq_A x y = true -> x = y.
Proof. now apply internal_option_dec_bl. Qed.
Lemma option_lb {A eq_A} (A_lb : forall x y : A, x = y -> eq_A x y = true) {x y} 
  : x = y -> option_beq A eq_A x y = true.
Proof. now apply internal_option_dec_lb. Qed.
```

Comment author: @JasonGross

[Scheme Equality for bool.] prints:

bool_beq is defined
bool_eq_dec is defined

Perhaps internal_bool_dec_bl and internal_bool_dec_lb should be renamed to strip off the internal_ prefix, and should be printed in the output?


Comment author: @silene

Renaming is not a solution since it would break existing developments. I guess we could add shorter-named synonyms though.


Comment author: @herbelin

Don't know if my comment will help as long as it is just a comment and no acting, but my view about these inductive schemes is more and more that they should enter a name space canonically bound to the inductive type (e.g. Coq.Init.Datatypes.bool.beq, Coq.Init.Datatypes.bool.eq_dec, ...) seen as a name space virtually populated with all possible schemes, and the schemes generated lazily on demand, with no need for Scheme Equality, nor for names with prefix "internal_". Just referring to bool.beq should force the effective construction of the scheme.

Making it a comment though to see if this possibly echoes the view of others.


Comment author: @silene

I am fine with the general idea of generating schemes on the fly (though this does not solve the issue of naming and statement of the theorems). But I always wonder how coqchk will deal with such generated definitions. Will the checker have to be modified to generate them on the fly or will they be redundantly stored inside the .vo files?


Comment author: @herbelin

Good question. This would complicate the checker to have it able to rebuild the schemes, so these schemes would still be put in the environment as it is now when using Scheme, and as it was in 8.4 when generated them internally. 
Then, they would be duplicated across different files, like it was for internal ones in 8.4, and like it is now for internal ones with the grain being the proof.

Wrt the stm, having them as "side-effect" does not look worse to me than when e.g. adding universe constraints produced by Qed proofs as a side-effect. This is actually better since universe constraints do not always have a consistent superset (in a way similar to what happened in pre-8.5 with file-level  modularity) while schemes always have a consistent superset. Maybe another motivation to get rid of "spaghetti" code and rolling up the side-effects produced by commands e.g. at the level of at least vernacentries.ml...


Comment author: @aspiwack

Generating schemes on the fly is kind of orthogonal to having them tucked in a dedicated namespace. I'm quite in favour of the latter, while I'm not entirely sure what benefits on-the-fly generation brings in most cases.

It also requires strong commutation properties -- which, as Hugo notes in [BZ#7](https://github.com/coq/coq/issues?q=is%3Aissue%20%22Original%20bug%20ID%3A%20BZ%237%22), is acceptable -- as the schemes could be generated in two different files, and requiring both files would add the scheme twice to the environment. It's probably not an easy thing to do.


