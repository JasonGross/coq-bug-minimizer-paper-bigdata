Comment author: @JasonGross

The current behavior of Coq is confusing, and makes working with multiple scopes a pain.  Here is some contrived code demonstrating the counter-intuitive behavior, with comments explaining what I expect.
```coq
Inductive nat1 := O1 | S1 (_ : nat1).
Inductive nat2 := O2 | S2 (_ : nat2).
Axiom add1 : nat1 -> nat1 -> nat1.
Axiom add2 : nat2 -> nat2 -> nat2.
Axiom conv12 : nat1 -> nat2.
Axiom conv21 : nat2 -> nat1.
Delimit Scope nat1_scope with nat1.
Delimit Scope nat2_scope with nat2.
Bind Scope nat1_scope with nat1.
Bind Scope nat2_scope with nat2.
Notation "1" := (S1 O1) : nat1_scope.
Notation "1" := (S2 O2) : nat2_scope.
Notation "++++++" := (O1) : nat1_scope.
Notation "a + b" := (add1 a b) : nat1_scope.
Notation "a + b" := (add2 a b) : nat2_scope.
Notation "a +F b" := (add1 a%nat1 b%nat1) (at level 50, left associativity) : nat1_scope.
Notation "a +F b" := (add2 a%nat1 b%nat1) (at level 50, left associativity) : nat2_scope.

(** verify that bind scope worked *)
Check conv12 (1 : nat1).
Fail Check conv12 (1 : nat2).
Check conv21 (1 : nat2).
Fail Check conv21 (1 : nat1).
(* all correct *)

(** I expect that a bound argument scope will bring that scope to the
    top for that argument; if [foo] has one argument with scope
    [bar_scope] (delimited by [bar]), [foo x] should be equivalent to
    [foo (x)%bar]. *)

(** verify that bound scopes take precedence over explicit scopes above them *)
Check (conv12 1)%nat2.
Check (conv12 1 +F conv12 1)%nat2.
Check (conv12 1)%nat1.
Check (conv12 (conv21 (conv12 1) +F conv21 (conv12 1)))%nat2.
Check (conv12 (conv21 (conv12 1) +F conv21 (conv12 1)))%nat1.
(** so far so good *)
Check (conv12 ((fun _ => 1) tt)%nat1). (* good *)
Fail Check (conv12 ((fun _ => 1) tt)). (* bad!  [1] should be interpreted in [nat1_scope] automatically *)
Check (conv12 ((fun _ => 1) tt +F (fun _ => 1) tt)). (* but if we use a notation that forces the scopes, things work... *)
Fail Check (conv12 ((fun _ => 1) tt + (fun _ => 1) tt)). (* ... but not if the notation assumes its scopes automatically *)
Check (conv12 (fst (1, tt)))%nat1. (* good *)
Fail Check conv12 (fst (1, tt)). (* bad!  [1] should be interpreted in [nat1_scope] automatically *)
(** Furthermore, implicit scopes should be layered, and certainly should not be overwritten by empty scopes *)
Check (conv12 ++++++). (* good *)
Check (conv12 ++++++%nat1). (* good *)
Check (conv12 (++++++%nat2)%nat1). (* good *)
Check (conv12 (++++++%nat1)%nat2). (* good *)
Fail Check (conv21 (fst (1%nat2, ++++++))). (* bad! the [fst (_, _)] should not remove the implicit scopes above it! *)
```

Comment author: @JasonGross

See https://github.com/HoTT/HoTT/pull/708, and, in particular, the remaining explicit `%equiv / %path` scopes in the diff for that, for a more realistic example of how this makes things more painful.  (Basically, I can think of ~no use to have a function argument have the scope-effect of "remove all implicitly bound scopes", while there are many uses for "add this scope to the stack".)

If there is worry about breaking backward compatibility, I'd be ok with a `Set Separate Implicit Scope Stack` (or similar) compatibility flag.


Comment author: @charguer

On a related not, I have add the very confusing error:

"Error: P is here used in the empty scope stack while it was elsewhere used in scope heap_scope"

```coq
Definition heap_is_star (H1 H2 : hprop) : hprop := ...
Open Scope heap_scope.
Bind Scope heap_scope with hprop.
Delimit Scope heap_scope with h.
Notation "P ==>+ Q" := (pred_le P (heap_is_star P Q)) 
  (at level 55) : heap_scope_advanced.
```
 
which I could (finally) solve by adding (non-intuitive) explicit scopes:
```coq
Notation "P ==>+ Q" := (pred_le P%h (heap_is_star P Q)) 
  (at level 55) : heap_scope_advanced.
```

Maybe the change log should explain how to work around this kind of new errors, and explain why it is raised in the first place.


Comment author: @JasonGross

Arthur, that is unrelated.  Please open a separate bug report if you want that documented.  However, it is not a new error.  For reference, here is code that works in 8.4 but not trunk:
```coq
Axiom hprop : Type.
Axiom heap_is_star : hprop -> hprop -> hprop.
Bind Scope heap_scope with hprop.
Open Scope heap_scope.
Delimit Scope heap_scope with h.
Axiom not_hprop : Type.
Axiom pred_le : not_hprop -> hprop -> hprop.
Notation "P ==>+ Q" := (pred_le P (heap_is_star P Q))
  (at level 55) : heap_scope_advanced.
```
The issue is that, in trunk, `Bind Scope` is retroactive.  This is a good thing, even if it causes backwards incompatibilities.  Here is code that fails with the relevant error message in both 8.4 and trunk:
```coq
Axiom hprop : Type.
Bind Scope heap_scope with hprop.
Axiom heap_is_star : hprop -> hprop -> hprop.
Open Scope heap_scope.
Delimit Scope heap_scope with h.
Axiom not_hprop : Type.
Axiom pred_le : not_hprop -> hprop -> hprop.
Notation "P ==>+ Q" := (pred_le P (heap_is_star P Q))
  (at level 55) : heap_scope_advanced.
```

Comment author: @andrew-appel

More "Bind Scope" problems; please advise me whether this is the same bug or whether I should file a separate bug report.  (This bug occurs in 8.4pl6, and presumably has been there forever).
```coq
(* Let "eee" be an expression with notation from scope s *)

Bind Scope s with t.
Definition force_t (x: t) := x.

Definition x : t := (...).   (* FAILS *)
Definition x := force_t (...).  (* succeeds *)
```
So, the BUG (in my opinion) is that the type constraint
(in the failing definition) should be sufficient to activate
the "bind scope", but it is not sufficient.
-- A. Appel


Comment author: @silene

It is a different bug. It is also fixed in 8.5. Please submit a fresh bug report if you have testcases that still fail.


Comment author: @jhjourdan

I also encountered this problem with 8.5pl2 :
```coq
Delimit Scope scope with S.

Notation "#" := 0 : scope.

Definition foo (n:nat) := n.
Arguments foo _%S.
Check (foo #).

Definition bar (n:nat -> nat) := n 0.
Arguments bar _%S.
Fail Check (bar (fun _ => #)).
Check (bar (fun _ => #)%S).
```

Comment author: @herbelin

I had missed these multiple reports about scopes when they arrived. As far as I can summarize, there are 3 questions:

Andrew's question is already solved in 8.5.
Arthur's question is answered by Jason.
Jason's and Jacques-Henri's questions are open.

I'm missing experience myself to evaluate whether implicit scopes should open the scope only for the immediate subterm (as it is now) or if it should remain open for subterms of the immediate subterms too, so that it works when traversion a lambda, or a projection (and more generally any kind of term construct).

I'm ready to provide an option to support this alternative behavior, with one exception: If I have a term in position of type, the scope type_scope is automatically open, but I don't think it is legitimate to keep it open for the subterms of the type, besides those subterms statically known as types, since these subterms have otherwise little chance to be types themselves (consider e.g. "fun H : a*b=0 => ..." where I don't want the "*" to be interpreted in type scope).

A prototype with this feature is currently available at https://github.com/herbelin/github-coq/tree/v8.6+persistent-implicit-scopes with name "Unset Immediate Only Implicit Scope".

Incidentally, that would certainly be useful to think eventually at a notion of scope aware of types, but that seems a more complex issue since unification would have then to be mixed with interpretation of notations.


Comment author: @jhjourdan

Coming back to your example "fun H : a*b=0 => ...", my intuition would say that what should be done is making the parameters of eq have their scope bound to C_scope, so that a*b is not interpreted in type_scope.


Comment author: @JasonGross

What is C_scope, and why should equality activate it?

Hugo, I think a more generic solution would be to have a specific modifier that means "pop the top of the scope stack if it's X", and then bind this "untype_scope" for eq and JMeq.  Can you think of any other combinators that deserve this behavior?


